defmodule Artemis.Drivers.IBMCloudant.CreateSearch do
  alias Artemis.Drivers.IBMCloudant

  @moduledoc """
  Enables full text search by adding search design document and search index to
  a database.

  See https://cloud.ibm.com/docs/services/Cloudant?topic=cloudant-design-documents#indexes-design-docs
  """

  @default_design_doc "text-search"
  @default_index "text-search-index"

  def call(schema) do
    database_config = IBMCloudant.Config.get_database_config_by!(schema: schema)
    host_config = IBMCloudant.Config.get_host_config_by!(name: database_config[:host])

    call(host_config, database_config)
  end

  def call(host_config, database_config) do
    database_schema = Keyword.fetch!(database_config, :schema)

    case search_enabled_on_host?(host_config) do
      true -> get_or_create_search(host_config, database_schema)
      false -> {:ok, "Search not enabled on host"}
    end
  end

  # Helpers

  defp search_enabled_on_host?(host_config) do
    host_config
    |> Keyword.fetch!(:search_enabled)
    |> String.downcase()
    |> String.equivalent?("true")
  end

  defp get_or_create_search(host_config, database_schema) do
    cloudant_host = database_schema.get_cloudant_host()
    cloudant_path = database_schema.get_cloudant_path()
    search_fields = database_schema.search_fields()
    options = get_options(host_config)

    {:ok, document} = IBMCloudant.GetOrCreateDesignDocument.call(cloudant_host, cloudant_path, options[:design_doc])

    current_indexes = Map.get(document, "indexes", %{})
    current_index_key = Keyword.get(options, :index)
    current_search_function = Artemis.Helpers.deep_get(current_indexes, [current_index_key, "index"])
    search_function = generate_search_index(search_fields)

    case search_function == current_search_function do
      true -> {:ok, "Search index already exists"}
      false -> create_search_index(cloudant_host, cloudant_path, document, current_indexes, search_function, options)
    end
  end

  defp get_options(host_config) do
    [
      design_doc: Keyword.get(host_config, :search_design_doc, @default_design_doc),
      index: Keyword.get(host_config, :search_index, @default_index)
    ]
  end

  defp generate_search_index(fields) do
    fields_without_default = List.delete(fields, :_id)

    clauses =
      Enum.map(fields_without_default, fn field ->
        """
          if (typeof(doc.#{field}) !== 'undefined') {
            index("#{field}", doc.#{field});
          }
        """
      end)

    """
    function (doc) {
      // Warning
      //
      // Automatically generated by operational dashboard
      // Changes will be overwritten

      index("default", doc._id);

      #{Enum.join(clauses, "\n")}
    }
    """
  end

  defp create_search_index(host, path, document, current_indexes, search_function, options) do
    search_index = Map.put(%{}, options[:index], %{"analyzer" => "standard", "index" => search_function})
    indexes = Map.merge(current_indexes, search_index)
    body = Map.put(document, "indexes", indexes)
    index_path = "#{path}/_design/#{options[:design_doc]}"

    IBMCloudant.Request.call(%{
      body: Jason.encode!(body),
      host: host,
      method: :put,
      path: index_path
    })
  end
end
