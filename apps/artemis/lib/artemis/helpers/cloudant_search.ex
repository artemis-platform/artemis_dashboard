defmodule Artemis.Helpers.CloudantSearch do
  alias Artemis.Drivers.IBMCloudant

  # TODO: refactor as part of data migration manager
  # TODO: get values from config

  @cloudant_search_design_doc "text-search"
  @cloudant_search_index "text-search-index"

  def enable_search(schema, search_fields) do
    cloudant_host = schema.get_cloudant_host()
    cloudant_path = schema.get_cloudant_path()

    {:ok, document} = get_or_create_search_document(cloudant_host, cloudant_path)

    current_indexes = Map.get(document, "indexes", %{})
    current_search_function = Artemis.Helpers.deep_get(current_indexes, [@cloudant_search_index, "index"])
    search_function = generate_search_index(search_fields)

    case search_function == current_search_function do
      true -> {:ok, "Search index already exists"}
      false -> create_search_index(cloudant_host, cloudant_path, document, current_indexes, search_function)
    end
  end

  # Helpers

  defp get_or_create_search_document(host, path) do
    case get_search_document(host, path) do
      {:error, %{"error" => "not_found"}} -> create_search_document(host, path)
      response -> response
    end
  end

  defp get_search_document(host, path) do
    IBMCloudant.call(%{
      host: host,
      method: :get,
      path: "#{path}/_design/#{@cloudant_search_design_doc}"
    })
  end

  defp create_search_document(host, path) do
    {:ok, _} =
      IBMCloudant.call(%{
        body: "{}",
        host: host,
        method: :put,
        path: "#{path}/_design/#{@cloudant_search_design_doc}"
      })

    get_search_document(host, path)
  end

  defp generate_search_index(fields) do
    fields_without_default = List.delete(fields, :_id)

    clauses =
      Enum.map(fields_without_default, fn field ->
        """
          if (typeof(doc.#{field}) !== 'undefined') {
            index("#{field}", doc.#{field});
          }
        """
      end)

    """
    function (doc) {
      // Warning
      //
      // Automatically generated by Dashboard Application
      // Changes will be overwritten

      index("default", doc._id);

      #{Enum.join(clauses, "\n")}
    }
    """
  end

  defp create_search_index(host, path, document, current_indexes, search_function) do
    search_index = Map.put(%{}, @cloudant_search_index, %{"analyzer" => "standard", "index" => search_function})
    indexes = Map.merge(current_indexes, search_index)
    body = Map.put(document, "indexes", indexes)
    index_path = "#{path}/_design/#{@cloudant_search_design_doc}"

    IBMCloudant.call(%{
      body: Jason.encode!(body),
      host: host,
      method: :put,
      path: index_path
    })
  end
end
